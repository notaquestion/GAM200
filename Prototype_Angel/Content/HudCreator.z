class HudCreator : ZilchComponent
{
    // The level that our Hud is contained within
    [Property]
    var HudLevel : Level = null;
    [Property]
    var SpaceArchetype : Archetype = Archetype.Find("Space");
    
    // Store the space where our Hud is so that we can access and destroy it
    var HudSpace : Space = null;
    var Init : Boolean = false;
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    }
    
    function OnLogicUpdate(e : UpdateEvent)
    {
        if(this.Init == true)
        {
            return;
        }
        
        this.Init = true;
        // Create the hud space and then load our hud level into it
        this.HudSpace = this.GameSession.CreateNamedSpace("HudSpace", this.SpaceArchetype);
        this.HudSpace.LoadLevel(this.HudLevel);
    }
    
    function Destroyed()
    {
        // Make sure to clean up the Hud space when we get destroyed
        if(this.HudSpace != null)
        {
            this.HudSpace.Destroy();
        }
    }
}
